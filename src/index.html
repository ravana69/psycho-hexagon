<script id="fragmentShader" type="x-shader/fragment">
precision highp float;
uniform float time;
uniform float width;
uniform float height;

const float PI = 3.141592654;
const float DEG = PI / 180.0;

vec2 coords() {
  float vmin = min(width, height);  
  return vec2((gl_FragCoord.x - width * .5) / vmin, 
              (gl_FragCoord.y - height * .5) / vmin);
}  

vec2 rotate(vec2 p, float a) {
  return vec2(p.x * cos(a) - p.y * sin(a),
              p.x * sin(a) + p.y * cos(a));
}

vec2 repeat(in vec2 p, in vec2 c) {
  return mod(p, c) - 0.5 * c;
}

// Distance functions by Inigo Quilez  
// https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float circle(in vec2 p, in vec2 pos, float radius) {
  return length(p - pos) - radius;
}

float box(in vec2 p, in vec2 pos, in vec2 b) {
  vec2 d = abs(p - pos) - b;
  return length(max(d, vec2(0))) + min(max(d.x, d.y), 0.0);
}

float triangle(in vec2 p, in float h) {
  const float k = sqrt(3.0);
  p.x = abs(p.x) - h;
  p.y = p.y + h / k;
  if( p.x + k*p.y > 0.0 ) p = vec2(p.x-k*p.y,-k*p.x-p.y)/2.0;
  p.x -= clamp( p.x, -2.0, 0.0 );
  return -length(p)*sign(p.y);
}  
  
float hexagon(in vec2 p, in vec2 pos, in float r) {
  const vec3 k = vec3(-0.866025404,0.5,0.577350269);
  p = abs(p - pos);
  p -= 2.0*min(dot(k.xy,p),0.0)*k.xy;
  p -= vec2(clamp(p.x, -k.z*r, k.z*r), r);
  return length(p) * sign(p.y);
}

float hexagram(in vec2 p, in vec2 pos, in float r) {
  const vec4 k=vec4(-0.5,0.8660254038,0.5773502692,1.7320508076);
  p = abs(p - pos);
  p -= 2.0*min(dot(k.xy,p),0.0)*k.xy;
  p -= 2.0*min(dot(k.yx,p),0.0)*k.yx;
  p -= vec2(clamp(p.x,r*k.z,r*k.w),r);
  return length(p)*sign(p.y);
}
  
float distanceField(vec2 p) {
  float hexa = hexagon(p, vec2(0, 0), 0.7);
  float box = box(p, vec2(0, 0), vec2(0.6));
  float x = pow((1.0 + sin(time * .5)) * .5, 2.0);
  return mix(hexa, box, x);
}
  
vec3 shade(in vec2 p, in vec3 background) {
  float sdf = distanceField(p);
  vec3 fill = vec3(sin(p.x * 0.2 * 10.0 + time * .25), clamp(cos(time * 2.5 - sdf * 20.0) * .5, .0, .25) * 4.0, sin(p.y * 0.5 + time * .25));
  return sdf < 0.0 ? fill : background;
}


void main () {
  vec2 p00 = coords();
  vec2 p0 = p00;
  float zoom = 5.0;
  vec2 p1 = rotate(p0 * zoom, 45.0 * DEG);
  //vec2 p2 = rotate(repeat(p1, vec2(2.0, 2.0)), time * 8.0 * DEG);
  vec2 p4 = repeat(p1, vec2(2.0) * mix(p0.y * sin(time *.25), 1.0, -.25));
  vec3 background = vec3(0);
  vec3 col = shade(p4, background);
  gl_FragColor = vec4(col, 1.0);
}
</script>
<script id="vertexShader" type="x-shader/vertex">
precision mediump float;
attribute vec2 position;

void main () {
  gl_Position = vec4(position, 0, 1);
} 
</script>
<div class="ui">

</div>